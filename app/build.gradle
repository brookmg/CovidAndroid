apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

def keystorePropertiesFile = rootProject.file("./keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "ethiopia.covid19.android"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode appVersionMajor * 1_000_000 + appVersionMinor * 10_000 + appVersionBuild * 1_000 + iterationNumber
        versionName String.valueOf(appVersionMajor) + "." + String.valueOf(appVersionMinor) + "." + String.valueOf(appVersionBuild)
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../covid_release.jks")
            storePassword keystoreProperties['releaseStorePassword']
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_api_key", keystoreProperties['googleMapsAPIKKey']
        }
        debug {
            //applicationIdSuffix '.debug'
            FirebasePerformance {
                // Set this flag to 'false' to disable @AddTrace annotation processing and
                // automatic HTTP/S network request monitoring
                // for a specific build variant at compile time.
                instrumentationEnabled false
            }
            versionNameSuffix ' (' + gitBranch() + ')'
            resValue "string", "google_maps_api_key", keystoreProperties['googleMapsAPIKKey']
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    buildFeatures.viewBinding = true
    buildFeatures.dataBinding = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation('com.github.bumptech.glide:glide:4.11.0') { exclude group: "com.android.support" }
    implementation "androidx.palette:palette:1.0.0"
    implementation 'com.github.ibrahimsn98:SmoothBottomBar:1.7.2'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    // maybe we should use https://github.com/skydoves/ExpandableLayout instead
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.mikhaellopez:circularprogressbar:3.0.3'
    implementation 'com.github.mumayank:AirLocation:1.3.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.github.franmontiel:LocaleChanger:1.0'

    debugImplementation 'com.github.ChuckerTeam.Chucker:library:3.2.0'
    releaseImplementation 'com.github.ChuckerTeam.Chucker:library-no-op:3.2.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'

    implementation 'com.github.armcha:AutoLinkTextView:0.3.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'com.github.stfalcon:stfalcon-imageviewer:1.0.1'
    implementation 'com.google.android.exoplayer:exoplayer:2.9.5'
    implementation 'im.ene.toro3:toro:3.7.0.2010003'
    implementation 'im.ene.toro3:toro-ext-exoplayer:3.7.0.2010003'

    // KTX
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation "androidx.preference:preference-ktx:1.1.1"

    //FIREBASE
    implementation firebase.core
    implementation firebase.config
    implementation firebase.performance
    implementation firebase.messaging
    implementation firebase.crashlytics
    implementation (firebase.firestore)
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.maps.android:android-maps-utils:1.0.2'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.6'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
}
apply plugin: 'com.google.gms.google-services'